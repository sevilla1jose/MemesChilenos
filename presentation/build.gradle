plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

def env = new Properties()
if (rootProject.file("env.properties").exists())
    env.load(new FileInputStream(rootProject.file("env.properties")))

android {
    compileSdk 32

    testBuildType "develop"

    defaultConfig {
        applicationId "com.jsevilla.memeschilenos"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        develop {
            keyAlias env['keyAliasDevelop']
            keyPassword env['keyPasswordDevelop']
            storeFile file(env['storeFileDevelop'])
            storePassword env['storePasswordDevelop']
        }

        release {
            keyAlias env['keyAliasRelease']
            keyPassword env['keyPasswordRelease']
            storeFile file(env['storeFileRelease'])
            storePassword env['storePasswordRelease']
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }

        qa {
            applicationIdSuffix ".qa"
            versionNameSuffix "-qa"
            debuggable false
            minifyEnabled false

            signingConfig signingConfigs.develop
        }

        develop {
            applicationIdSuffix ".develop"
            versionNameSuffix "-develop"
            debuggable true
            minifyEnabled false

            signingConfig signingConfigs.develop
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'debug')
            setIgnore(true)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def navigationDependencies = rootProject.ext.navigationDependencies
    def presentationTestImplementationDependencies = rootProject.ext.presentationTestImplementationDependencies

    implementation presentationDependencies.values()
    implementation navigationDependencies.values()

    kapt rootProject.ext.compileLifecycleDependencies.lifecycleCompiler

    testImplementation testDependencies.values()
    androidTestImplementation presentationTestImplementationDependencies.androidXEspresso

    implementation project(":data")
    implementation project(":domain")
}